// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Screen.jack

/**
 * A library of functions for displaying graphics on the screen.
 * The Hack physical screen consists of 512 rows (indexed 0..511, top to bottom)
 * of 256 pixels each (indexed 0..255, left to right). The top left pixel on 
 * the screen is indexed (0,0).
 */
class Screen {

    static Array mask;
    static Array leftMask;
    static Array rightMask;
    static boolean currentColor;
    static boolean BLACK;
    static boolean WHITE;
    static int screen;

    /** Initializes the Screen. */
    function void init() {
        var int i;
        let mask = Array.new(16);
        let mask[0] = 1;
        let i = 1;
        while(i < 16){
            let mask[i] = mask[i - 1] + mask[i - 1];
            let i = i + 1;
        }

        let rightMask = Array.new(16);
        let rightMask[0] = 0;
        let rightMask[1] = 1;
        let rightMask[2] = 3;
        let rightMask[3] = 7;
        let rightMask[4] = 15;
        let rightMask[5] = 31;
        let rightMask[6] = 63;
        let rightMask[7] = 127;
        let rightMask[8] = 255;
        let rightMask[9] = 511;
        let rightMask[10] = 1023;
        let rightMask[11] = 2047;
        let rightMask[12] = 4095;
        let rightMask[13] = 8191;
        let rightMask[14] = 16383;
        let rightMask[15] = 32767;

        let leftMask = Array.new(16);
        let leftMask[0] = 0;
        let leftMask[1] = -(32767 + 1);
        let leftMask[2] = -16384;
        let leftMask[3] = -8192;
        let leftMask[4] = -4096;
        let leftMask[5] = -2048;
        let leftMask[6] = -1024;
        let leftMask[7] = -512;
        let leftMask[8] = -256;
        let leftMask[9] = -128;
        let leftMask[10] = -64;
        let leftMask[11] = -32;
        let leftMask[12] = -16;
        let leftMask[13] = -8;
        let leftMask[14] = -4;
        let leftMask[15] = -2;
        
        let screen = 16384;
        let BLACK = true;
        let WHITE =  false;
        do Screen.setColor(BLACK);
        return;
    }

    /** Erases the entire screen. */
    function void clearScreen() {
        var int screenMemorySize, i;
        let screenMemorySize = 8 * 1024; // 8K graphics memory
        while(i < screenMemorySize){
            let screen[i] = 0; 
            let i = i + 1;
        }
        return;
    }

    /** Sets the current color, to be used for all subsequent drawXXX commands.
     *  Black is represented by true, white by false. */
    function void setColor(boolean b) {
        let currentColor = b;
        return;
    }

    function int mod(int a, int b){
        return a - ((a / b) * b);
    }

    function int getWordAddress(int x, int y){
        return (32 * y) + (x / 16);
    }

    /** Draws the (x,y) pixel, using the current color. */
    function void drawPixel(int x, int y) {
        var int value, mod, address;
        let address = Screen.getWordAddress(x, y);
        let value = screen[address];
        let mod = Screen.mod(x, 16);
        if(currentColor = BLACK){
            let value = value | mask[mod];
        }else{
            let value = value & (~mask[mod]);
        }
        let screen[address] = value;
        return;
    }

    /** Draws a line from pixel (x1,y1) to pixel (x2,y2), using the current color. */
    function void drawLine(int x1, int y1, int x2, int y2) {
        if(x1 = x2){
            do Screen.drawVerticalLine(y1, y2, x1);
        }else{
            if(y1 = y2){
                do Screen.drawHorizontalLine(x1, x2, y1);
            }else{
                if(Screen.isAscendingLine(x1, y1, x2, y2)){
                    do Screen.drawAscendingLine(x1, y1, x2, y2);
                }else{
                    if(Screen.isDescendingLine(x1, y1, x2, y2)){
                        do Screen.drawDescendingLine(x1, y1, x2, y2);
                    }
                }
            }
        }
        return;
    }

    function boolean isAscendingLine(int x1, int y1, int x2, int y2){
        if(y1 < y2){
            return x1 > x2;
        }else{
            return x2 > x1;
        }
    }

    function boolean isDescendingLine(int x1, int y1, int x2, int y2){
        if(y1 < y2){
            return x2 > x1;
        }else{
            return x1 > x2;
        }
    }

    function void drawDescendingLine(int x1, int y1, int x2, int y2){
        var int dx, dy, a, b, diff, x, y;
        let dx = Math.abs(x2 - x1); let dy = Math.abs(y2 - y1);
        let a = 0; let b = 0; let diff = 0;
        if(y1 < y2){
            let y = y1; let x = x1;
        }else{
            let y = y2; let x = x2;
        }
        while(((a < dx) | (a = dx)) & ((b < dy) | (b = dy))){
            do Screen.drawPixel(x + a, y + b);
            if(diff < 0){
                let a = a + 1;
                let diff = diff + dy;
            }else{
                let b = b + 1;
                let diff = diff - dx;
            }
        }
        return;
    }

    function void drawAscendingLine(int x1, int y1, int x2, int y2){
        var int dx, dy, a, b, diff, x, y;
        let dx = Math.abs(x2 - x1); let dy = Math.abs(y2 - y1);
        let a = 0; let b = 0; let diff = 0;
        if(y1 > y2){
            let y = y1; let x = x1;
        }else{
            let y = y2; let x = x2;
        }
        while(((a < dx) | (a = dx)) & ((b < dy) | (b = dy))){
            do Screen.drawPixel(x + a, y - b);
            if(diff < 0){
                let a = a + 1;
                let diff = diff + dy;
            }else{
                let b = b + 1;
                let diff = diff - dx;
            }
        }
        return;
    }

    function void drawHorizontalLine(int x1, int x2, int y){
        var int fromAddress, toAddress, value;
        let fromAddress = Screen.getWordAddress(x1, y);
        let toAddress = Screen.getWordAddress(x2, y);

        if(fromAddress = toAddress){
            while((x1 < x2) | (x1 = x2)){
                do Screen.drawPixel(x1, y);
                let x1 = x1 + 1;
            }
        }else{
            if(currentColor = BLACK){
                let screen[fromAddress] = screen[fromAddress] | leftMask[Screen.mod(x1, 16)];
                let fromAddress = fromAddress + 1;
                while(fromAddress < toAddress){
                    let screen[fromAddress] = -1;
                    let fromAddress = fromAddress + 1;
                }
                let screen[fromAddress] = screen[fromAddress] | rightMask[Screen.mod(x2, 16)];
            }else{
                let screen[fromAddress] = screen[fromAddress] & (~leftMask[Screen.mod(x1, 16)]);
                let fromAddress = fromAddress + 1;
                while(fromAddress < toAddress){
                    let screen[fromAddress] = 0;
                    let fromAddress = fromAddress + 1;
                }
                let screen[fromAddress] = screen[fromAddress] & (~rightMask[Screen.mod(x2, 16)]);
            }
        }
        
        return;
    }

    function void drawVerticalLine(int y1, int y2, int x){
        var int fromY, toY;
        let fromY = Math.min(y1, y2);
        let toY = Math.max(y1, y2);
        while((fromY < toY) | (fromY = toY)){
            do Screen.drawPixel(x, fromY);
            let fromY = fromY + 1;
        }
        return;
    }

    /** Draws a filled rectangle whose top left corner is (x1, y1)
     * and bottom right corner is (x2,y2), using the current color. */
    function void drawRectangle(int x1, int y1, int x2, int y2) {
        var int lineLen, nLines, x, y;
        let lineLen = Math.abs(x1 - x2);
        let nLines = Math.abs(y1 - y2);
        let x = Math.min(x1, x2);
        let y = Math.min(y1, y2);
        while(nLines > 0){
            do Screen.drawHorizontalLine(x, x + lineLen, y);
            let y = y + 1;
            let nLines = nLines - 1;
        }
        return;
    }

    /** Draws a filled circle of radius r<=181 around (x,y), using the current color. */
    function void drawCircle(int x, int y, int r) {
        var int dy, sqrt;
        let dy = -r;
        if((r < 181) | (r = 181)){
            while((dy < r) | (dy = r)){
                let sqrt = Math.sqrt((r * r) - (dy * dy));
                do Screen.drawHorizontalLine(x - sqrt, x + sqrt, y + dy);
                let dy = dy + 1;
            }
        }
        return;
    }
}
